#autoload
zmodload -F zsh/parameter p:functions

_autocomplete.recent_paths() {
  (( _matcher_num > 1 )) &&
    return 1

  local -aU reply=()
  local tag
  _tags recent-directories recent-files
  while _tags; do

    tag=recent-directories
    _requested $tag &&
      [[ -v functions[_autocomplete.recent_dirs] && $_comp_tags == *( | local-)directories* ]] &&
      _autocomplete.recent_dirs "$PREFIX$SUFFIX" &&
      .autocomplete.recent_paths.add $tag 'recent directory' $reply[@]

    tag=recent-files
    _requested $tag &&
      [[ -v functions[_autocomplete.recent_files] && $_comp_tags == *( | all-)'files '* ]] &&
      _autocomplete.recent_files "$PREFIX$SUFFIX" &&
      .autocomplete.recent_paths.add $tag 'recent file' $reply[@]
  done

  return 1  # Always continue to next completer.
}

.autocomplete.recent_paths.add() {
  setopt localoptions NO_autonamedirs

  local -a displ expl
  local pref _path _comp_no_ignore=yes
  _description -V $1 expl $2
  shift 2
  for _path in $@; do
    displ=( "${(D)_path}" )
    pref=$_path:h/
    [[ $pref == // ]] &&
      pref=/
    compadd "$expl[@]" -d displ -P $pref -fW $pref - "$_path:t"
  done
}

_autocomplete.recent_paths "$@"
