#!/bin/zsh
zmodload -Fa zsh/zutil b:zstyle
builtin autoload -Uz is-at-least
typeset -g ZLE_REMOVE_SUFFIX_CHARS=$' ;\n\r\t'
typeset -g ZLE_SPACE_SUFFIX_CHARS=$'|&<>'

.autocomplete.config.precmd() {
  # Remove incompatible settings.
  builtin zstyle -d ':completion:*:functions' ignored-patterns
  builtin zstyle -d ':completion:*:*:*:*:*' menu
  builtin zstyle -d '*' single-ignored
  builtin zstyle -d ':completion:*' special-dirs
  builtin zstyle -d ':completion:*:default' list-prompt
  unset LISTPROMPT
}

builtin zstyle ':completion:*' use-cache yes
builtin zstyle -e ':completion:*' cache-path _autocomplete.config.cache-path
_autocomplete.config.cache-path() {
  reply=( "${XDG_CACHE_HOME:-$HOME/.cache}/zsh/compcache" )
}

builtin zstyle -e ':completion:*' completer _autocomplete.config.completer
_autocomplete.config.completer() {
  if [[ CURRENT -eq 1 && -z $PREFIX$SUFFIX && $compstate[context] == command ]]; then
    reply=( _complete )
  else
    reply=( _expand _complete _correct _complete:-fuzzy _ignored )
  fi
}
builtin zstyle ':completion:list-expand:*' completer \
    _expand _complete:-fuzzy _ignored
builtin zstyle ':completion:history-incremental-*search-*:*' completer \
    _autocomplete.history_lines

builtin zstyle -e ':completion:*' range _autocomplete.config.range
_autocomplete.config.range() {
  [[ -z $PREFIX$SUFFIX ]] &&
      reply=( 16 )
}
builtin zstyle ':completion:*' remove-all-dups yes

builtin zstyle ':completion:*:expand:*' tag-order '! all-expansions original' -
builtin zstyle -e ':completion:*:-command-:*' tag-order _autocomplete.config.tag-order
_autocomplete.config.tag-order() {
    private -aU tags=()
    [[ -n "$path[(r).]" ]] &&
        tags+=( globbed-files executables directories )
    [[ -o autocd ]] &&
        tags+=( '(|local-)directories' )
    [[ -n $tags ]] &&
        tags+=( suffix-aliases )
    reply=( "$tags" )
    if [[ -z $PREFIX$SUFFIX ]]; then
      reply+=( - )
    else
      reply+=(
        'aliases functions builtins reserved-words commands'
      )
    fi
}
builtin zstyle ':completion:list-expand:*:-command-:*' tag-order '*'
builtin zstyle ':completion:*:-tilde-:*' tag-order directory-stack
builtin zstyle ':completion:*:(approximate|correct):*' tag-order '! original' -

builtin zstyle ':completion:*:git-*:(|*-)argument-*:*' tag-order \
    '! (|cached-)files *-remote remote-* (|*-)tags' \
    '! *-remote remote-* tags'
builtin zstyle ':completion:list-expand:*:git-*:(|*-)argument-*:*' tag-order '*'

# Order of matchers matters: m should come before r, which should come before l.
# Otherwise, the results are not as expected.
builtin zstyle ':completion:*' matcher-list \
    'm:{[:lower:]-}={[:upper:]_} r:|[.]=** r:?|[-_]=* l:?|=[-_\\ ]'
builtin zstyle ':completion:*-fuzzy:*' matcher-list \
    'm:{[:lower:]-}={[:upper:]_} r:|?=**'

builtin zstyle ':completion:*:options' matcher 'b:-=+'

builtin zstyle ':completion:*' prefix-needed yes
builtin zstyle ':completion:*:expand:*' accept-exact continue
builtin zstyle ':completion:*:expand-alias:*' complete yes
builtin zstyle ':completion:*' ignore-parents 'parent pwd directory'

builtin zstyle ':completion:*:history-words' ignored-patterns '?' '[^[:IDENT:]]#'
builtin zstyle ':completion:*:users'         ignored-patterns '_*'
builtin zstyle ':completion:*:widgets'       ignored-patterns '.*' '*:*' '*orig-*'

builtin zstyle -e ':completion:*:expand:*' glob _autocomplete.is_glob
builtin zstyle ':completion:*:expand:*' keep-prefix no  # Needed for file type highlighting
builtin zstyle ':completion:*:expand:*' add-space subst

# Complete only the tail of a path.
builtin zstyle ':completion:*:paths' expand suffix
builtin zstyle ':completion:*:paths' list-suffixes yes
builtin zstyle ':completion:*:paths' path-completion no

builtin zstyle ':completion:*' group-name ''
builtin zstyle ':completion:*' group-order \
    expansions options \
    globbed-files local-directories directories executables suffix-aliases \
    aliases functions builtins reserved-words commands

builtin zstyle ':completion:*' complete-options yes
builtin zstyle ':completion:*' list-dirs-first yes

builtin zstyle ':completion:*:descriptions' format $'%{\e[1;2;39m%}%d%{\e[0m%}'
builtin zstyle ':completion:*:history-words' format ''
builtin zstyle ':completion:*:warnings' format '%F{11}missing %d%f'
builtin zstyle ':completion:*:messages' format '%F{9}%d%f'

builtin zstyle ':completion:*:history-lines' format ''

builtin zstyle ':completion:*' auto-description '%d'
builtin zstyle ':completion:*:parameters' extra-verbose yes
builtin zstyle ':completion:*:default' select-prompt '%F{black}%K{14}line %l %p%f%k'

builtin zstyle ':completion:*' insert-sections yes
builtin zstyle ':completion:*' separate-sections yes

# Needed for _gnu_generic to prevent descriptions from appearing cropped on wide screens.
is-at-least 5.8.1 ||
    builtin zstyle ':completion:*' command '- COLUMNS=999'
