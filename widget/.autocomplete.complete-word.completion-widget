#!/bin/zsh
local 0=${(%):-%N}

$0() {
  local 0=${(%):-%N}
  local +h curcontext=${curcontext:-${WIDGET}:::}
  local +h -a comppostfuncs=( $0.post "$comppostfuncs[@]" )
  if  ( [[ "$KEYS" == "$key[Backtab]" ]] ||
        zstyle -t ":autocomplete:${(kL)key[(Re)$KEYS]}:" insert-unambiguous
      ) && [[ $_lastcomp[iprefix]$_lastcomp[prefix]$_lastcomp[suffix]$_lastcomp[isuffix]
                != *$_lastcomp[unambiguous]* ]]; then
    compstate[old_list]=
    _main_complete
  elif [[ -n $compstate[old_list] ]]; then
    compstate[old_list]=keep
    _main_complete -
  fi
  return 0  # Prevent beeping.
}

$0.post() {
  unset curcontext
  if  ( [[ "$KEYS" == "$key[Backtab]" ]] ||
        zstyle -t ":autocomplete:${(kL)key[(Re)$KEYS]}:" insert-unambiguous
      ) && [[ $IPREFIX$PREFIX$SUFFIX$ISUFFIX != *$compstate[unambiguous]* ]]; then
    compstate[insert]=${${(M)WIDGET:#*menu-*}:+automenu-}unambiguous
  else
    compstate[insert]=
    if [[ $WIDGET == *menu-* ]]; then
      compstate[insert]=menu
      [[ $WIDGET == *-select ]] &&
        MENUSELECT=0
    fi
    compstate[insert]+="${${${KEYS:#$key[Backtab]}:+1}:-0}${${CURRENT:#1}:+ }"
  fi
}

$0 "$@"
