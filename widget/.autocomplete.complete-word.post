#autoload
unset curcontext
compstate[list]=

if  ( [[ $KEYS == $key[Backtab] ]] ||
      zstyle -t ":autocomplete:${(kL)key[(Re)$KEYS]}:" insert-unambiguous
    ) && [[ $IPREFIX$PREFIX$SUFFIX$ISUFFIX != *${compstate[unambiguous]:#?}* ]]; then
  compstate[insert]=${${(M)WIDGET:#*menu-*}:+automenu-}unambiguous
  return
fi

compstate[insert]=
unset MENUSELECT

if [[ $WIDGET == *menu-select ]]; then
  # Determine which terminal line we're on (for async completion).
  (( _autocomplete__buffer_start_line = max( BUFFERLINES, LINES - compstate[list_lines] ) ))
  MENUSELECT=0
fi

if [[ $KEYS == $key[Backtab] ]]; then
  compstate[insert]+=0
else
  compstate[insert]+=1
fi

local -a tags; zstyle -a :autocomplete: add-space tags ||
  tags=( executables aliases functions builtins reserved-words commands )
if [[ $RBUFFER != [[:space:]]* && -n ${${=${_comp_tags:-$_lastcomp[tags]}}:*tags} ]]; then
  [[ $WIDGET == *menu-* ]] &&
      compstate[insert]="menu:$compstate[insert]"
  compstate[insert]+=' '
fi
