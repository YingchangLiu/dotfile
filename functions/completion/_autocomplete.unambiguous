#autoload

[[ -v _autocomplete__partial_list ]] &&
    return 1

(( compstate[nmatches] > 1 )) ||
  return 1
[[ $curcontext == history-*:* ||
    $IPREFIX$PREFIX$SUFFIX$ISUFFIX == *${compstate[unambiguous]:#?}* ]] &&
  return 1

if builtin zstyle -t :autocomplete:tab: insert-unambiguous; then
  private -i nxt= prv=1
  private subs= cols=
  for nxt in ${(s.:.)compstate[unambiguous_positions]}; do
    subs+='('${(b)compstate[unambiguous][prv,nxt]}')*'
    cols+='=30;103'  # black foreground, bright yellow background
    (( prv = ++nxt ))
  done
  ZLS_COLORS="=(#bi)*$subs=$cols:$ZLS_COLORS"
  return 1
fi

private tag=unambiguous
_tags $tag
_tags && _requested $tag &&
    builtin compadd -J "$tag" \
        -x $'%{\e[1;2;39m%}common substring:%b %F{0}%K{11}'"$compstate[unambiguous]%f%k"
