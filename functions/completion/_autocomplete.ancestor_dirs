#autoload
(( compstate[nmatches] )) &&
    return 1

[[ $_comp_tags == *( | local-)'directories '* ]] ||
    return

private tag=ancestor-directories
_tags $tag
_tags ||
    return
_requested $tag ||
    return

private -a ancestors
private -i ret=1
private dir=$PWD:h
while (( $#dir > 1 )); do
  ancestors=( $dir $ancestors[@] )
  dir=$dir:h
done

local displ expl
while _next_label -V $tag expl "ancestor directory"; do
  private dir
  for dir in $ancestors[@]; do
    displ=( "$dir" )
    [[ -z $PREFIX$SUFFIX ]] &&
        displ=( "$dir:t" )
    compadd "$expl[@]" -d displ -P "${${(D)dir:h}%/}/" -fW "${${dir:h}%/}/" \
        - "$dir:t"
  done
done

return 1  # Always continue to next completer.
