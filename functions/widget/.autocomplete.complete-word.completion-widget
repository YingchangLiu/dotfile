#!/bin/zsh
zmodload -F zsh/terminfo p:terminfo

local -a match=() mbegin=() mend=()  # `zstyle` for some reason assigns these.
local +h curcontext=${curcontext:-${WIDGET}:::}
local +h -a comppostfuncs=( .autocomplete.complete-word.post "$comppostfuncs[@]" )

if [[ -z $compstate[old_list] ]] ||
    [[ -v _autocomplete__partial_list && $WIDGETSTYLE == (|*-)(list|menu)(|-*) ]] ||
    { [[ -n $_autocomplete__unambiguous ]] &&
        builtin zstyle -t ":autocomplete:${WIDGET}:" insert-unambiguous }; then
  autocomplete:new:_main_complete
else
  compstate[old_list]=keep
  autocomplete:new:_main_complete -
fi

(( _lastcomp[nmatches] )) && [[ -n $compstate[insert] ]] &&
    unset curcontext
