#!/bin/zsh
local 0=${(%):-%N}
builtin zle -C ${0}.context list-choices ${0}.context

${0}.context() {
  # Inside a completion widget, the current quote level is removed. So, we should not include
  # $QIPREFIX/$QISUFFIX in our pattern or it won't match.
  lcontext=${LBUFFER%${(~j:*:)words[1,CURRENT-1]}*$IPREFIX$PREFIX}
  lcontext+=${lcontext:+ }${(pj:\0:)${(@b)words}}
  rcontext=${RBUFFER#$SUFFIX$ISUFFIX*${(~j:*:)words[CURRENT+1,-1]}}
}

${0}() {
  setopt $_autocomplete__comp_opts[@] $_autocomplete__ctxt_opts[@]

  local -P lbuffer="$LBUFFER" rbuffer="$RBUFFER"

  local lcontext= rcontext=
  builtin zle ${(%):-%N}.context
  {
    LBUFFER="$lcontext"
    RBUFFER="$rcontext"

    [[ -o sharehistory ]] &&
        fc -RI  # Get new history events from file.
    builtin zle _history_search

  } always {
    if [[ $BUFFER == $lcontext$rcontext ]] then
      LBUFFER="$lbuffer"
      RBUFFER="$rbuffer"
    fi
  }
}

$0 "$@"
